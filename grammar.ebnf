statements = [ [statement-delimiter], statement, { statement-delimiter, [ statement ] } ];
statement = expr | define;
statement-delimiter = ? /(\n|;;)+/ ?;

define-variable = "let", identifier, "=", expr;
define-function = "let", identifier, ":", variables, function-expr;
define = define-variable | define-function;

function-assign = "->", expr;
function-clause = "when", expr, function-assign;
function-expr = function-assign |
                ( [ clause-delimiter ], function-clause, { clause-delimiter, function-clause } );
clause-delimiter = [ "\n" ], "|";

variables = identifier, { ",", identifier };

expr-calc = term, [ ( "+" | "-" ) expr-calc ];
term = factor, [ ( "*" | "/" ), term ];
factor = [ "-" ], par;
par = ( "(", expr, ")" ) | number | identifier;

bool-operator =  "=" | ">" | "<" | ">=" | "<=" | "!="; 
expr-bool = expr, bool-operator, expr;

expr = expr-calc | expr-bool;

number-int = ? /[0-9_]+/ ?;
number-point = ? /([0-9_]*)?\.[0-9_]+/ ?;
number = number-int | number-point;

identifier = ? /[a-zA-Z][a-zA-Z_]*/ ?;

comment = ? /#[^\n]*\n/ ?
